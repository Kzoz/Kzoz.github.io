1 '*****************************************************************
2 '* CognexEZ ビジョン 自動キャリブレーション(ハンドカメラ:RV)
3 '*****************************************************************
4 Dim POFS(5),CSEL$(4),PVS(5),PFB(5),PAngle(4),PT(4)
5 '*****************************************************************
6 '*****************************************************************
7 '
8 '1.パラメータ設定
9 '　NVTRGTMG：1
10 '　ネットワーク設定:COM2に設定
11 '
12 '2.ビジョン設定
13 '　①Calibration.jobをビジョンに保存する
14 '　②シンボリックタグの設定
15 '　③IPアドレスの設定
16 '　④認識マークの登録
17 '
18 '3.動作設定
19 '　P0にキャリブレーション開始位置を教示(Tool=0で教示する)
20 PVSize.X = 1600              'ビジョンのX方向ピクセル値
21 PVSize.Y = 1200              'ビジョンのY方向ピクセル値
22 PAngle(1).C = Rad(3)         'ツール算出時C軸回転量 1回目
23 PAngle(2).C = Rad(20)        'ツール算出時C軸回転量 2回目
24 PAngle(3).C = Rad(90)        'ツール算出時C軸回転量 3回目
25 PAngle(4).C = Rad(90)        'ツール算出時C軸回転量 4回目
26 POFS(2).X = -10              'キャリブレーション移動量X-　※ツール方向
27 POFS(3).X = +10              'キャリブレーション移動量X+　※ツール方向
28 POFS(4).Y = -10              'キャリブレーション移動量Y-　※ツール方向
29 POFS(5).Y = +10              'キャリブレーション移動量Y+　※ツール方向
30 '
31 '*****************************************************************
32 '*****************************************************************
33 '*** Teaching ***
34 P0=P0                        '基準位置(画面中心付近とする)
35 '*** MDI ***
36 PVSize=PVSize                'ビジョン解像度(X,Y)
37 PAngle(1)=PAngle(1)          'カメラ動かす角度1(C) ※カメラ視野から外れない値とする
38 PAngle(2)=PAngle(2)          'カメラ動かす角度2(C)
39 PAngle(3)=PAngle(3)          'カメラ動かす角度3(C)
40 PAngle(4)=PAngle(4)          'カメラ動かす角度4(C)
41 POFS(2)=POFS(2)              'キャリブレーション移動量(-X) ※カメラ視野から外れない値とする
42 POFS(3)=POFS(3)              'キャリブレーション移動量(+X) ※カメラ視野から外れない値とする
43 POFS(4)=POFS(4)              'キャリブレーション移動量(-Y) ※カメラ視野から外れない値とする
44 POFS(5)=POFS(5)              'キャリブレーション移動量(+Y) ※カメラ視野から外れない値とする
45 '
46 *Init
47 PVCenter=P_Zero              'ビジョン中心を設定
48 PVCenter.X=PVSize.X/2        'X座標
49 PVCenter.Y=PVSize.Y/2        'Y座標
50 ''PVCenter.Y=PVCenter.Y*(-1)  '(座標手系を合わせる)
51 CCOM$="COM2:"                '回線オープンする回線番号設定
52 CPRG$="Calibration.job"      'ビジョンジョブ名を設定
53 GoSub *SVSOPEN               'ビジョン回線オープン
54 Servo On
55 Tool P_NTool
56 Mov P0                       '基準位置(*教示*)
57 '
58 *Main
59 '//1.カメラ角度･スケールを算出
60 GoSub *SCalcAngleScale       'カメラ角度･スケールを算出
61 '
62 '//2.マークをカメラ中心に移動
63 PCS=P0                       '初期位置(退避点)を設定
64 GoSub *SMovCenter            'マークがカメラ中心になるように移動する
65 PC0=PCE                      '中心位置を保存
66 '
67 '//3.ツールデータ(PTL)算出
68 PTL=P_NTool                  '初期ツールデータ
69 For M1=1 To 4
70 Tool PTL
71 PC1=PC0*PTL                  '現在ツールでの中心位置を算出
72 PCS=PC1*PAngle(M1)           '初期位置(角度ふった位置)を設定
73 GoSub *SMovCenter            'マークがカメラ中心になるように移動する
74 PC2=PCE
75 GoSub *SCalcTool             'ツールデータ算出
76 PTL=PNEWTL
77 PT(M1)=PNEWTL                '確認用
78 Next M1
79 P_02=PTL
80 Tool P_NTool
81 '
82 '//4.ビジョン キャリブレーションリセット
83 GoSub *SVSCalibReset         'ビジョンN点キャリブレーションリセット
84 '
85 '//5.キャリブレーション
86 '
87 For M2=1 To 5
88 Mov PC0*POFS(M2)             '撮像位置へ移動
89 Dly 0.5
90 GoSub *SVSTRG                'マーク撮像
91 GoSub *SVSSETPS              'ポイント設定
92 Next M2
93 '
94 '//6.ビジョン キャリブレーションエクスポート
95 GoSub *SVSCalibExport        'ビジョンN点キャリブレーションエクスポート
96 '
97 ' Tool P_NTool
98 Hlt
99 End
100 '
101 '### カメラ角度･スケール算出処理 ###
102 *SCalcAngleScale
103 '//1点目撮像
104 Mvs P0*POFS(2)
105 Dly 0.5
106 GoSub *SVSTRG                'マーク撮像
107 PVS1=PV
108 ''PVS1.Y=PVS1.Y*(-1)          '(座標手系を合わせる)
109 '//2点目撮像
110 Mvs P0*POFS(3)
111 Dly 0.5
112 GoSub *SVSTRG                'マーク撮像
113 PVS2=PV
114 ''PVS2.Y=PVS2.Y*(-1)          '(座標手系を合わせる)
115 '//カメラ角度算出
116 MDX=PVS1.X-PVS2.X
117 MDY=PVS1.Y-PVS2.Y
118 MVSC=-Atn2(MDY,MDX)
119 PVSC=P_Zero
120 PVSC.C=MVSC
121 '//スケール値算出
122 MPL=Sqr(MDX*MDX+MDY*MDY)
123 MML=Abs(POFS(3).X-POFS(2).X)
124 MSCale=MML/MPL
125 Return
126 '
127 '### マークがカメラ中心になるように移動 ###
128 *SMovCenter
129 '//中心位置撮像
130 MRTRY=0                      'リトライカウンタ初期化
131 MFIN=0                       '終了フラグを初期化
132 PC=PCS
133 While MFIN<>1
134 Mov PC
135 Dly 0.5
136 GoSub *SVSTRG                'マーク撮像
137 PVSH=PV
138 ''  PVSH.Y=PVSH.Y*(-1)        '(座標手系を合わせる)
139 GoSub *SCalcCenter           '中心位置までの補正量を算出
140 PC=PC*PH
141 If MDH<=0.02 Then            '補正量が規定値(0.02mm)以内なら
142 MFIN=1                       '処理終了
143 Else
144 If MRTRY>3 Then              'リトライ3回より多くなったら
145 Error 9100
146 MRTRY=0
147 Else
148 MRTRY=MRTRY+1
149 EndIf
150 EndIf
151 WEnd
152 PCE=PC                       'マーク中心位置
153 Return
154 '
155 '### ビジョン認識結果(PVSH)から中心位置(PVCenter)までの補正座標(PH),補正距離(MDH)を算出 ###
156 *SCalcCenter
157 PHPXY=P_Zero                 'ビジョン座標系での補正量(pix)
158 PHPXY.X=PVSH.X-PVCenter.X
159 PHPXY.Y=PVSH.Y-PVCenter.Y
160 PHP=PVSC*PHPXY               'ツール座標系での補正量(pix)
161 PH=P_Zero                    'ツール座標系での補正量(mm)
162 PH.X=PHP.X*MSCale
163 PH.Y=PHP.Y*MSCale
164 MDH=Sqr(PH.X*PH.X+PH.Y*PH.Y) '補正量
165 Return
166 '
167 '### ツールデータ算出処理 ###
168 *SCalcTool
169 PTMP=Inv(PC2)*PC1
170 '
171 NX=Cos(PTMP.C)
172 NY=Sin(PTMP.C)
173 OX=-Sin(PTMP.C)
174 OY=Cos(PTMP.C)
175 MX=PTMP.X
176 MY=PTMP.Y
177 '
178 PDT=P_Zero
179 PDT.X=((1-OY)*MX+OX*MY)/((1-NX)*(1-OY)-NY*OX)
180 PDT.Y=(NY*MX+(1-NX)*MY)/((1-NX)*(1-OY)-NY*OX)
181 '
182 PNEWTL=P_Tool*PDT
183 'Tool PNEWTL
184 Return
185 '
186 '
187 '###  ﾋﾞｼﾞｮﾝ回線ｵｰﾌﾟﾝ処理 ###
188 *SVSOPEN
189 If M_NvOpen(1)=1 Then
190 NVClose                      '回線ｸﾛｰｽﾞ
191 Wait M_NvOpen(1)<>1
192 EndIf
193 Dly 1
194 'NVOpen CCOM$ As #1           '回線ｵｰﾌﾟﾝ+ﾛｸﾞｵﾝ
195 NVOpen "COM2:" As #1           '回線ｵｰﾌﾟﾝ+ﾛｸﾞｵﾝ
196 Wait M_NvOpen(1)=1           'ﾋﾞｼﾞｮﾝｾﾝｻへﾛｸﾞｵﾝ待ち
197 NVLoad #1,CPRG$              'ﾋﾞｼﾞｮﾝﾌﾟﾛｸﾞﾗﾑをﾛｰﾄﾞする
198 Return
199 '
200 '### ビジョン認識処理 ###
201 *SVSTRG
202 NVRun #1,CPRG$               'ビジョン起動
203 EBRead #1,,MNUM1,PV1         '認識結果の取込み
204 If MNUM1=1 Then GoTo *SVS_OK
205 Error 9100
206 GoTo *SVSTRG
207 *SVS_OK
208 PV=PV1
209 Return
210 '
211 '### ビジョン キャリブレーションポイント設定 ###
212 *SVSSETPS
213 CNO$=Str$(M2-1)
214 PFBC=P_Fbc
215 CSEL$(1)="Calib_1.Pixel_Point"+CNO$+".X"
216 CSEL$(2)="Calib_1.Pixel_Point"+CNO$+".Y"
217 CSEL$(3)="Calib_1.World_Point"+CNO$+".X"
218 CSEL$(4)="Calib_1.World_Point"+CNO$+".Y"
219 MXX=-POFS(M2).X
220 MYY=-POFS(M2).Y
221 EBWrite #1,CSEL$(3),MXX
222 EBWrite #1,CSEL$(4),MYY
223 PVS(M2)=PV                   '確認用
224 PFB(M2)=PFBC                 '確認用
225 Return
226 '
227 '### ビジョン キャリブレーションリセット ###
228 *SVSCalibReset
229 CCEL$="Calib_1.External_Reset"
230 EBWrite #1,CCEL$,1
231 Dly 0.1
232 EBWrite #1,CCEL$,0
233 Return
234 '
235 '### ビジョン キャリブレーションエクスポート ###
236 *SVSCalibExport
237 CCEL$="Calib_1.File_Name"
238 EBWrite #1,CCEL$,"robot"
239 Dly 0.1
240 CCEL$="Calib_1.External_Export"
241 EBWrite #1,CCEL$,1
242 Dly 0.1
243 EBWrite #1,CCEL$,0
244 Return
245 '
POFS(1)=(+0.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
POFS(2)=(-10.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(7,0)
POFS(3)=(+10.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
POFS(4)=(+0.00,-10.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
POFS(5)=(+0.00,+10.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
PVS(1)=(+798.96,+599.96,+0.00,+0.00,+0.00,+100.01,+0.00,+0.00)(0,0)
PVS(2)=(+803.34,+469.80,+0.00,+0.00,+0.00,+100.02,+0.00,+0.00)(0,0)
PVS(3)=(+795.58,+732.35,+0.00,+0.00,+0.00,+100.02,+0.00,+0.00)(0,0)
PVS(4)=(+932.12,+604.66,+0.00,+0.00,+0.00,+100.01,+0.00,+0.00)(0,0)
PVS(5)=(+667.45,+596.80,+0.00,+0.00,+0.00,+100.02,+0.00,+0.00)(0,0)
PFB(1)=(+636.87,+156.55,+357.23,+180.00,+0.00,+180.00)(7,0)
PFB(2)=(+646.87,+156.54,+357.23,+180.00,+0.00,+180.00)(7,0)
PFB(3)=(+626.87,+156.54,+357.23,-180.00,+0.00,+180.00)(7,0)
PFB(4)=(+636.87,+146.54,+357.23,+180.00,+0.00,+180.00)(7,0)
PFB(5)=(+636.87,+166.55,+357.23,+180.00,+0.00,-180.00)(7,0)
PAngle(1)=(+0.00,+0.00,+0.00,+0.00,+0.00,+3.00,+0.00,+0.00)(7,0)
PAngle(2)=(+0.00,+0.00,+0.00,+0.00,+0.00,+20.00,+0.00,+0.00)(0,0)
PAngle(3)=(+0.00,+0.00,+0.00,+0.00,+0.00,+90.00,+0.00,+0.00)(0,0)
PAngle(4)=(+0.00,+0.00,+0.00,+0.00,+0.00,+90.00,+0.00,+0.00)(0,0)
PT(1)=(+69.23,-6.05,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
PT(2)=(+68.09,-5.93,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
PT(3)=(+68.76,-5.44,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
PT(4)=(+68.76,-5.45,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
PVSize=(+1600.00,+1200.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(7,0)
P0=(+626.81,+152.54,+357.23,-180.00,+0.00,+180.00)(7,0)
PVCenter=(+800.00,+600.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
PCS=(+568.11,+151.10,+357.23,+180.00,+0.00,+90.00)(7,0)
PC0=(+636.87,+156.54,+357.23,+180.00,+0.00,-180.00)(7,0)
PCE=(+568.12,+151.10,+357.23,-180.00,+0.00,+90.00)(7,0)
PTL=(+68.76,-5.45,+0.00,+0.00,+0.00,+0.00)(0,0)
PC1=(+568.11,+151.10,+357.23,+180.00,+0.00,-180.00)(7,0)
PC2=(+568.12,+151.10,+357.23,-180.00,+0.00,+90.00)(7,0)
PNEWTL=(+68.76,-5.45,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
PVS1=(+852.00,+602.03,+0.00,+0.00,+0.00,+100.01,+0.00,+0.00)(0,0)
PV=(+667.45,+596.80,+0.00,+0.00,+0.00,+100.02,+0.00,+0.00)(0,0)
PVS2=(+844.58,+864.24,+0.00,+0.00,+0.00,+100.02,+0.00,+0.00)(0,0)
PVSC=(+0.00,+0.00,+0.00,+0.00,+0.00,+88.38,+0.00,+0.00)(0,0)
PC=(+568.12,+151.10,+357.23,-180.00,+0.00,+90.00)(7,0)
PVSH=(+800.11,+600.00,+0.00,+0.00,+0.00,+100.01,+0.00,+0.00)(0,0)
PH=(+0.00,+0.01,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
PHPXY=(+0.11,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
PHP=(+0.00,+0.11,+0.00,+0.00,+0.00,+88.38,+0.00,+0.00)(0,0)
PTMP=(+0.00,-0.01,+0.00,+0.00,+0.00,-90.00)(7,0)
PDT=(+0.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(0,0)
PV1=(+667.45,+596.80,+0.00,+0.00,+0.00,+100.02,+0.00,+0.00)(0,0)
PFBC=(+636.87,+166.55,+357.23,+180.00,+0.00,-180.00)(7,0)
p1=(+0.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00,+0.00)(,)
